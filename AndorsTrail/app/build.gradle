apply plugin: 'com.android.application'

android {
    lintOptions{
        baseline file("lint.xml")
        abortOnError true
    }

    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.gpl.rpg.AndorsTrail"
        minSdkVersion 14
        targetSdkVersion 31
    }

    buildTypes {
        release {
            manifestPlaceholders icon_name: 'icon', fileproviderPath: 'AndorsTrail'
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            manifestPlaceholders icon_name: 'icon_beta', fileproviderPath: 'AndorsTrail.beta2'
            applicationIdSuffix 'beta2'
        }
    }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
}

task copyRes(type: Copy) {
    description "Copies the res folder to the modules res folder (& renames .tmx to .xml)"
    from "${rootDir}/res"
    into "${projectDir}/src/main/res"
    rename "(.*)\\.tmx", "\$1.xml"
}

task copyTranslation(type: Copy) {
    description("Copies the translation files to the modules translations folder")
    from "${rootDir}/assets/translation"
    into "${projectDir}/src/main/assets/translation"
}

task cleanup(type: Delete) {
    description("Deletes the assets/translation and the res folder from the modules folder")
    delete "${projectDir}/src/main/res", "${projectDir}/src/main/assets/translation"
}

afterEvaluate {
    preBuild.dependsOn project.tasks.copyRes
    preBuild.dependsOn project.tasks.copyTranslation
    clean.dependsOn project.tasks.cleanup
}
